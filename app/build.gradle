apply plugin: 'com.android.application'

/* gets the version name from the latest Git tag */
def getGitVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "co.copperhead.pdfviewer"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-" + getGitVersionName()
        }
    }

    /* set the debug versionCode based on how many commits in the repo */
    applicationVariants.all { variant ->
        if (variant.buildType.isDebuggable()) {
            // default to a timestamp, in case anything fails later
            variant.mergedFlavor.versionCode = new Date().getTime() / 1000
            try {
                def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
                    standardOutput = stdout
                }
                variant.mergedFlavor.versionCode = Integer.parseInt(stdout.toString().trim())
            }
            catch (ignored) {
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
